#!/usr/bin/perl

################################################################
#
#  Licensed to the Apache Software Foundation (ASF) under one
#  or more contributor license agreements.  See the NOTICE file
#  distributed with this work for additional information
#  regarding copyright ownership.  The ASF licenses this file
#  to you under the Apache License, Version 2.0 (the
#  "License"); you may not use this file except in compliance
#  with the License.  You may obtain a copy of the License at
#  
#  http://www.apache.org/licenses/LICENSE-2.0
#  
#  Unless required by applicable law or agreed to in writing,
#  software distributed under the License is distributed on an
#  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
#  KIND, either express or implied.  See the License for the
#  specific language governing permissions and limitations
#  under the License.
#
################################################################

use strict;
use Getopt::Long;
use File::Glob;
use File::Find;
use IO::File;
use Cwd 'abs_path';

use vars qw/ %opt /;


##########################################################################
# PrintHelp(): Prints the script usage.
##########################################################################

my $cmd;
my $logfile = "run.log";
my $simulator;
my @defines;
my @filelists;
my $uvm;
my @simargs;
my @compileargs;
my $outdir = ".";
my $clean;
my $help;

sub usage () {
  print "Usage:  runSVUnit -s|--sim <simulator> [-l|--log <log> -d|--define <macro> -f|--filelist <file> -U|-uvm -r|--r_arg <option> -c|--c_arg <option> -o|--out <dir>]\n";
  print "  -s|--sim <simulator>     : simulator is either of questa, modelsim, riviera, ius or vcs\n";
  print "  -l|--log <log>           : simulation log file (default: run.log)\n";
  print "  -d|--define <macro>      : appended to the command line as +define+<macro>\n";
  print "  -f|--filelist <file>     : some verilog file list\n";
  print "  -r|--r_arg <option>      : specify additional runtime options\n";
  print "  -c|--c_arg <option>      : specify additional compile options\n";
  print "  -U|--uvm                 : run SVUnit with UVM\n";
  print "  -o|--out                 : output directory for tmp and simulation files\n";
  print "  -h|--help                : prints this help screen\n";
  print "\n";
  exit 1;
}

sub clean () {
  find({
      wanted => sub {
                      if (/.*testsuite\.sv$/   or
                          /.*testrunner\.sv$/
                      ) {
                        unlink;
                      }
                    }
    }, qw( . ));

  unlink qw( run.log .svunit.f vsim.wlf ncsc.log irun.key );
  system("rm -rf work INCA_libs");
}

# command line options
GetOptions("l|log=s" => \$logfile,
           "s|sim=s" => \$simulator,
           "d|define=s" => \@defines,
           "f|filelist=s" => \@filelists,
           "U|uvm" => \$uvm,
           "r|r_arg=s" => \@simargs,
           "c|c_arg=s" => \@compileargs,
           "o|out=s" => \$outdir,
           "h|help" => \$help
           ) or usage();

usage() if $help;

clean();

# version
my $VERSION = IO::File->new();
$VERSION->open("$ENV{SVUNIT_INSTALL}/VERSION.txt") || die;
my $version = $VERSION->getline;
chomp $version;
push @defines, qq!SVUNIT_VERSION='"$version"'!;

# simulator check
$simulator =~ tr/A-Z/a-z/;
$simulator =~ s/questa/qverilog/;
$simulator =~ s/ius/irun/;
usage() if $simulator ne "qverilog" &&
           $simulator ne "irun" &&
           $simulator ne "modelsim" &&
           $simulator ne "riviera" &&
           $simulator ne "vcs";

# start the command line
$cmd = "cd $outdir; ";
if ($simulator eq "modelsim" or $simulator eq "riviera") {
  $cmd .= "vlib work; ";
  $cmd .= "vlog ";
} elsif ($simulator eq "vcs") {
  $cmd .= "$simulator -R -sverilog -l $logfile";
} else {
  $cmd .= "$simulator -l $logfile";
}

# add the uvm switches if necessary
if (defined $uvm) {
  $cmd .= " -uvm" if $simulator eq "irun";
  $cmd .= " -ntb_opts uvm" if $simulator eq "vcs";
  push @defines, "RUN_SVUNIT_WITH_UVM";
}

# add the filelists and defines
foreach (@filelists) {
  my $f = abs_path($_);
  $cmd .= " -f " . join "  -f ", $f;
}
$cmd .= " -f .svunit.f";

# defines
$cmd .= " +define+" . join " +define+", @defines if (@defines > 0);

if ($simulator eq "modelsim" or $simulator eq "riviera") {
  $cmd .= qq! @compileargs; vsim @simargs -lib work -c -do "run -all; quit" -l $logfile testrunner!;
} elsif ($simulator eq "qverilog") {
  $cmd .= " @compileargs -R @simargs -";
} else {
  $cmd .= " @compileargs @simargs";
}

# run!!
system("buildSVUnit -o $outdir");
print $cmd . "\n";
system("$cmd");
